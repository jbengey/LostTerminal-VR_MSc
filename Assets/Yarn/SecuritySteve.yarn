title: SecuritySteve.start
tags: 
colorID: 0
position: 624,-2054
---
<<if visited("SecuritySteve.start") is false>>
    <<playCharacterDialog SecuritySteve secruityGreeting>>  Alright treacle, What can I do for you?
<< elseif $securityRepeat is 1>>
    <<playCharacterDialog SecuritySteve securityFollowup>> Is there anything else I can do for you?
<< else >>
    <<playCharacterDialog SecuritySteve securitySecondgreet>> Oh Hi, how can I help?
<<endif>>



<<if visited("Receptionist.lostchild") is true>>
    [[ I have lost my child, I was directed to you for help|SecuritySteve.liaison]]
<< elseif $securityRepeatAns is not "flight" and visited("CheckInCarly.checkedin") is false >> 
    [[ I would like to board my flight|SecuritySteve.flight]]
<<endif>>

<< if visited("CafeWorkerDave.missingchild") is true>> 
    [[A suspicous man has taken my child!|SecuritySteve.cafe]]
<<endif>>

<< if visited("CheckInCarly.checkedin") is true and $securityRepeatAns is not "checkin">> 
    [[I'm here to get to my departure gate|SecuritySteve.checkin]]
<< endif>> 

[[ Nothing, Thank you|SecuritySteve.end]]
===
title: SecuritySteve.liaison
tags: 
colorID: 0
position: 995,-2317
---
<<playAnimatorState SecuritySteve Talking >>
Ahh! You must be the person that the announcement was about!
No problem at all, it's most likely that your child is with our passenger liaison, Larry..
He is just over there wearing a blue shirt, I am certain I saw a child with him earlier.
===
title: SecuritySteve.cafe
tags: 
colorID: 0
position: 1006,-1821
---
<<playAnimatorState SecuritySteve HangHead >>
h no no no...not again...
I am so sorry sir, this has been happening all week...
I am afraid to say that your child has been abducted...
//ENDING 3: CHILD WAS ABDUCTED
<< wait 2>>  
<< gameOver SecuritySteve>>
===
title: SecuritySteve.checkin
tags: 
colorID: 0
position: 991,-1574
---
<<playAnimatorState SecuritySteve AskForPass >>

<< if visited("CheckInCarly.checkedin") is true>> 
//If has boarding pass
    <<playAnimatorState SecuritySteve Thumbsup >>
    No problem at all Sir,
    your boarding pass looks good to me,
    please ensure you have no prohibited items in your hand luggage and enjoy your flight!
    << set $RetVal to 0 >> 
//ENDING 2: BOARD FLIGHT
<< wait 2>>  
<< gameOver SecuritySteve>> 


<<else>>
//If no boarding pass
    <<playAnimatorState SecuritySteve ShakeHead >>
    Sorry Sir, you need to have your boarding pass to proceed through the gate.
    <<set $securityRepeat to 1>>
    <<set $securityRepeatAns to "checkin">>
    << set $RetVal to 0 >> 

    [[SecuritySteve.start]]


<<endif>>
===
title: SecuritySteve.end
tags: 
colorID: 0
position: 336,-2051
---
<<playAnimatorState SecuritySteve Salute >>
<<playCharacterDialog SecuritySteve securityGoodbye>>  No problem, take care.
<<set $securityRepeatAns to 0>>
<<set $securityRepeat to 0>>
===
title: SecuritySteve.flight
tags: 
colorID: 0
position: 997,-2055
---
To get past security and catch your flight, you'll need your boarding pass!
Head over to the information point and they will be able to direct you to the right place to sort that!
<<set $securityRepeat to 1>>
<<set $securityRepeatAns to "flight">>
[[SecuritySteve.start]]
===
